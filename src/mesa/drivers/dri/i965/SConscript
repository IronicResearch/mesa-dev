###################################
# SConscript file for dri targets

Import('*')

#
# dri_i965
#
env = env.Clone()

env.Append(CPPPATH = [
    '/usr/include/libdrm',
    '#/include',
    '#/src/',
    '#/src/mapi',
    '#/src/mesa',
    '#/src/mesa/drivers/dri/common',
    xmlpool_options.dir.dir, # Dir to generated xmlpool/options.h
])

env.Append(CPPDEFINES = [
    'HAVE_DLADDR',
])

# LIBS to link driver for local Mesa imports
env.Prepend(LIBS = [
    libloader,
    mesautil,
    compiler,
    mesa,
    glsl,
    megadrivers_stub,
    dri_common,
])

# LIBS to link driver for DRM platform imports
env.Append(LIBS = ['drm','drm_intel','expat'])
env.Append(LIBPATH = ['/usr/lib/i386-linux-gnu'])

sources = env.ParseSourceList('Makefile.sources', ['i965_FILES','i965_compiler_FILES'])

dri_i965 = env.SharedLibrary(
	target = 'i965_dri.so',
	source = sources,
	SHLIBPREFIX = '',
)

env.Alias('dri_i965', dri_i965)

Export([
    'dri_i965',
])
